plugins {
    id "dev.gradleplugins.java-gradle-plugin" version "1.9.0"
    id "com.gradle.plugin-publish" version "1.2.2"
    id "jacoco"
    id "checkstyle"
}

description = "Plugin for publishing artifacts to Sonatype Maven Central using Portal Publisher API"

gradlePlugin {
    website = "https://github.com/boolivar/sonatype-portal-publish"
    vcsUrl = "https://github.com/boolivar/sonatype-portal-publish.git"
    compatibility {
        minimumGradleVersion = "6.0"
    }
    plugins {
        sonatypePortalPublish {
            id = "io.github.boolivar.sonatype-portal-publish"
            implementationClass = "org.bool.sonatype.gradle.SonatypePortalPublishPlugin"
            displayName = "Sonatype Central Portal publishing plugin"
            description = project.description
            tags.addAll("Sonatype", "Central Portal", "maven", "publish")
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            pom {
                name = "$groupId:$artifactId"
                description = project.description
                url = "https://github.com/boolivar/sonatype-portal-publish"
                inceptionYear = "2024"
                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                developers {
                    developer {
                        id = "boolivar"
                        name = "Aleksey Krichevskiy"
                        email = "boolivar@gmail.com"
                        organizationUrl = "https://github.com/boolivar"
                    }
                }
            }
        }
    }
}

checkstyle {
    configDirectory = file("$rootDir/gradle/config/checkstyle")
    toolVersion = "10.18.1"
}

tasks.named("test") {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named("jacocoTestCoverageVerification") {
    dependsOn test
    violationRules {
        rule {
            limit {
                minimum = jacocoMinRatio as BigDecimal
            }
        }
    }
}

tasks.named("check") {
    dependsOn jacocoTestCoverageVerification
}

tasks.named("jar") {
    dependsOn check
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation gradleTestKit()
    testImplementation "org.junit.jupiter:junit-jupiter:5.11.3"
    testImplementation "org.assertj:assertj-core:3.27.2"
    testImplementation "org.mockito:mockito-junit-jupiter:5.15.2"
    testImplementation "org.mock-server:mockserver-junit-jupiter:5.15.0"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}