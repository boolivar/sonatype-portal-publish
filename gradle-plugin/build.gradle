plugins {
    id "dev.gradleplugins.java-gradle-plugin" version "1.9.0"
    id "jacoco"
    id "checkstyle"
}

gradlePlugin {
    website = "https://github.com/boolivar/jdoc-test"
    vcsUrl = "https://github.com/boolivar/jdoc-test.git"
    compatibility {
        minimumGradleVersion = "5.4"
    }
    plugins {
        sonatypePortalPublish {
            id = "io.github.boolivar.sonatype-portal-publish"
            implementationClass = "org.bool.sonatype.gradle.SonatypePortalPublishPlugin"
            displayName = "Sonatype Portal Publisher"
            description = "Plugin for publishing artifacts to Sonatype Maven Central using Portal Publisher API"
            tags.addAll("Sonatype", "Central Portal", "maven", "publish")
        }
    }
}

checkstyle {
    configDirectory = file("$rootDir/gradle/config/checkstyle")
    toolVersion = "9.3"
}

tasks.named("test") {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named("jacocoTestCoverageVerification") {
    dependsOn test
    violationRules {
        rule {
            limit {
                minimum = jacocoMinRatio as BigDecimal
            }
        }
    }
}

tasks.named("check") {
    dependsOn jacocoTestCoverageVerification
}

tasks.named("jar") {
    dependsOn check
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:5.11.0"
    testImplementation "org.assertj:assertj-core:3.26.3"
    testImplementation "org.mockito:mockito-junit-jupiter:5.13.0"
    testImplementation "org.mock-server:mockserver-junit-jupiter:5.15.0"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}